buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.javamodularity:moduleplugin:1.6.0"
    }
}

group 'com.github.bredmold'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: "org.javamodularity.moduleplugin"

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    ext {
        versions = [
            /*
            Latest & greatest JUnit doesn't play nicely with the Gradle plugin we're using to manage modular
            compilation. Falling back to an older edition.
             */
            junit   : '5.3.1',

            /*
            Hamcrest 1.3 doesn't play nicely in a JPMS world. We need to update to version 2.1
             */
            hamcrest: '2.1',

            /*
            slf4j 1.8.x transitions from the static binder method for locating a logger implementation to service
            loaders. This makes it much more friendly in a JPMS world.

            Logback 1.3.x is required for JPMS-friendly behavior, and that requires slf4j 2.0.x. Hence, these new
            versions.
             */
            slf4j   : '2.0.0-alpha1',
            logback : '1.3.0-alpha4',
            log4j   : '2.12.1',
        ]
    }

    test {
        useJUnitPlatform()

        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${versions.junit}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"

        /*
        Gotta pull in the new & improved Hamcrest in order for it to play nicely in a JPMS application.
         */
        testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"
        testImplementation "org.hamcrest:hamcrest-library:${versions.hamcrest}"
        testImplementation "org.hamcrest:hamcrest-core:${versions.hamcrest}"

        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.13.2'
    }
}
